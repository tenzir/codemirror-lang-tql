@top Root {
  (Operator (Pipe Operator)*)?
}
Operator { OperatorName arg* }
OperatorName { Word }
arg {
  Word | 
  String |
  Punct |
  Integer |
  Float |
  Ip |
  Type |
  Meta | 
  Bool |
  Null |
  Parenthesis
}

@tokens {
  Word { $[a-zA-Z0-9-_,/:$]+ }
  String { "\"" !["]* "\""}
  Punct { "==" | "||" | "&&" | "=" | "!=" | "<" | "<=" | ">" | ">=" | "in" | "not" | "+" | "-" | "*" | "/" | "." }
  Parenthesis { $[()[\]{}] }
  Type { ":" $[a-zA-Z_]* }
  Meta { "#" $[a-zA-Z_]+ }
  Integer { "-"? $[0-9]+ }
  Float { "-"? @digit+ "." @digit+ }
  Bool { "true" | "false" }
  Null { "null" }
  Ip { @digit+ "." @digit+ "." @digit+ "." @digit+ ("/" @digit*)? }
  Pipe { "|" }

  whitespace { @whitespace }
  LineComment { ("//" | "# " ) ![\n]* }

  @precedence { "/*", LineComment, Type, Punct, Ip, Float, Integer, Null, Bool, Word }
}

@skip { whitespace | BlockComment | LineComment }

@skip {} {
  BlockComment { "/*" (blockCommentContent | blockCommentNewline)* blockCommentEnd }
}

@local tokens {
  blockCommentEnd { "*/" }
  blockCommentNewline { "\n" }
  @else blockCommentContent
}

@detectDelim

// @external propSource jsonHighlighting from "./highlight"
